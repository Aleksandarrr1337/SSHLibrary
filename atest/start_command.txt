*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login and Move Test Files
Suite Teardown  Remove Test Files and Close Connections
Resource        resources/ssh_library_resources.txt

*** Test Cases ***
Start Command And Read Process Output With Defaults
    Start Command  ./${TEST SCRIPT NAME}
    ${stdout} =  Read Command Output
    Should Be Equal  ${stdout}  This is stdout

Start Command And Read Only Return Code
    Start Command  ./${TEST SCRIPT NAME}
    ${rc} =  Read Command Output    return_stdout=${EMPTY}   return_rc=yes
    Should Be Equal As Integers  ${rc}  0

Start Command And Execute Command
    Start Command  ./${TEST SCRIPT NAME}
    ${stdout} =  Execute Command  ./${TEST SCRIPT NAME} Hello
    Should Be Equal  ${stdout}  Hello
    ${stdout} =  Read Command Output
    Should Be Equal  ${stdout}  This is stdout

Start Command And Read Process Output With Legacy Stdout
    Start Command  ./${TEST SCRIPT NAME}
    ${stdout} =  Read Command Output  STDOUT
    Should Be Equal  ${stdout}  This is stdout

Start Command And Read Process Output With Legacy Stderr
    Start Command  ./${TEST SCRIPT NAME}
    ${stderr} =  Read Command Output  stderr
    Should Be Equal  ${stderr}  This is stderr

Start Command And Read Process Output With Legacy Stdout And Stderr
    Start Command  ./${TEST SCRIPT NAME}
    ${stdout}  ${stderr} =  Read Command Output  both
    Should Be Equal  ${stdout}  This is stdout
    Should Be Equal  ${stderr}  This is stderr

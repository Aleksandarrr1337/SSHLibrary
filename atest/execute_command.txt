*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login And Move Test Files
Suite Teardown  Remove Test Files And Close Connections
Resource        resources/ssh_library_resources.txt
Library         OperatingSystem  WITH NAME  OS

*** Test Cases ***
Execute Command With Defaults
    ${stdout} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    Should Be Equal  ${stdout}  This is stdout

Execute Command And Return Stderr
    ${stderr} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...          return_stdout=false  return_stderr=yes
    Should Be Equal  ${stderr}  This is stderr

Execute Command And Return Rc
    ${rc} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...           return_stdout=False  return_rc=${true}
    Should Be Equal As Integers  ${rc}  0

Execute Command And Return All Values
    ${stdout}  ${stderr}  ${rc} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...                            foo  bar  baz
    Should Be Equal  ${stdout}  This is stdout
    Should Be Equal  ${stderr}  This is stderr
    Should Be Equal As Integers  ${rc}  0

Execute Command With Legacy Stdout Config
    ${stdout} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...                           STDout
    Should Be Equal  ${stdout}  This is stdout

Execute Command With Legacy Stderr Config
    ${stderr} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...                           stderr
    Should Be Equal  ${stderr}  This is stderr

Execute Command With Legacy Stdout And Stderr Config
    ${stdout}  ${stderr} =  Execute Command  ${REMOTE TEST ROOT}${/}${TEST SCRIPT NAME}
    ...                     Both
    Should Be Equal  ${stdout}  This is stdout
    Should Be Equal  ${stderr}  This is stderr

Execute Command With Output Containing Newlines
    ${res} =  Execute Command  cat ${REMOTE TEST ROOT}${/}${FILE WITH NEWLINES NAME}
    ${actual} =  OS.Get File  ${FILE WITH NEWLINES}
    Should Be Equal  ${res}\n  ${actual}

Execute Command With Multiple Statements
    ${result} =  Execute Command  echo "foo\n"; echo RC=$?
    Should Be Equal  ${result}  foo\n\nRC=0

Executing Command With Non-ASCII characters
    ${result}=  Execute Command  echo 'aaääöö'
    Should Contain  ${result}  aaääöö

*** Keywords ***
Login And Move Test Files
    Login As Valid User
    Put File  ${TEST SCRIPT}  ${REMOTE TEST ROOT}${/}
    Put File  ${FILE WITH NEWLINES}  ${REMOTE TEST ROOT}${/}

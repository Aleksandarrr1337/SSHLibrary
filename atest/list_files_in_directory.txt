*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login And Move Test Files
Suite Teardown  Remove Test Files and Close Connections
Resource        resources/ssh_library_resources.txt
Library         Collections

*** Test Cases ***
List Files Using Absolute Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}
    ...                         ${FILE WITH NON-ASCII NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${FILE WITH SPECIAL CHARS NAME}
    ...                         ${TEST FILE NAME}
    ${listing} =  List Files In Directory  ${REMOTE TEST ROOT}
    Lists Should Be Equal  ${listing}  ${expected}

List Files Using Relative Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}
    ...                         ${FILE WITH NON-ASCII NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${FILE WITH SPECIAL CHARS NAME}
    ...                         ${TEST FILE NAME}
    ${listing} =  List Files In Directory  ${TEST_ROOT_NAME}
    Lists Should Be Equal  ${listing}  ${expected}

List Files Using Current Directory
    ${listing} =  List Files In Directory  .
    Should Not Contain  ${listing}  ${TEST_ROOT_NAME}

List Files Using Symlink As Path
    [Setup]  Execute Command  ln -s ${REMOTE TEST ROOT} symlink
    ${expected} =  Create List  ${TEST FILE 2 NAME}
    ...                         ${FILE WITH NON-ASCII NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${FILE WITH SPECIAL CHARS NAME}
    ...                         ${TEST FILE NAME}
    ${listing} =  List Files In Directory  symlink
    Lists Should Be Equal  ${listing}  ${expected}
    [Teardown]  Execute Command  rm -f symlink

List Files With Absolute Paths Using Absolute Path
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NON-ASCII NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NEWLINES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH SPECIAL CHARS NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${listing} =  List Files In Directory  ${REMOTE TEST ROOT}  absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

List Files With Absolute Paths Using Relative
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NON-ASCII NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NEWLINES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH SPECIAL CHARS NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${listing} =  List Files In Directory  ${TEST_ROOT_NAME}  absolute=True
    Lists Should Be Equal  ${listing}  ${EXPECTED}

List Files With Absolute Paths Using Current Directory
    ${listing} =  List Files In Directory  .  absolute=True
    Should Not Contain  ${listing}  ${TEST_ROOT_NAME}

List Files Using Pattern
    ${expected} =  Create List  ${FILE WITH NEWLINES NAME}
    ${listing} =  List Files In Directory  ${REMOTE TEST ROOT}  pattern=*newlines.txt
    Lists Should Be Equal  ${listing}  ${expected}

List Files With Absolute Paths Using Pattern
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${listing} =  List Files In Directory  ${REMOTE TEST ROOT}  pattern=?est*
    ...           absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

List Files Should Fail When Source Path Not Exists
    ${target} =  Set Variable  not_exists
    Run Keyword And Expect Error  There was no path matching '${target}'.
    ...                           List Files In Directory  ${target}

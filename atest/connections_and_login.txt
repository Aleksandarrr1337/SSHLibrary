*** Settings ***
Default Tags      pybot   jybot
Test Teardown   Close All Connections
Resource        resources/ssh_library_resources.txt

*** Test Cases ***
Test Opening And Closing Connection
    [Setup]  Open Connection  ${HOST}
    Login  ${USERNAME}  ${PASSWORD}

Switch Connection
    ${id1} =  Open Connection  ${HOST}
    Login  ${USERNAME}  ${PASSWORD}
    Write  cd /tmp
    Read Until Prompt
    ${id2} =  Open Connection  ${HOST}  second
    Login  ${USERNAME}  ${PASSWORD}
    ${prev id} =  Switch Connection  ${id1}
    Should Be Equal  ${prev id}  ${id2}
    Write  pwd
    ${result} =  Read Until Prompt
    Should Contain  ${result}  /tmp
    ${prev id} =  Switch Connection  second
    Write  pwd
    ${result} =  Read Until Prompt
    Should Contain  ${result}  /home

Login Returns Banner Value
    [Setup]  Open Connection  ${HOST}
    ${ret}=  Login  ${USERNAME}  ${PASSWORD}
    Should Contain  ${ret}  Last login

Enable Logging
    [Tags]    pybot
    [Setup]  Remove File  ${OUTPUTDIR}/sshlog.txt
    Enable SSH Logging  ${OUTPUTDIR}/sshlog.txt
    Open Connection  ${HOST}
    Login  ${username}  ${password}
    File Should Not Be Empty  ${OUTPUTDIR}/sshlog.txt

Executing Command Before Opening Connection
    Run Keyword And Expect Error  No open connection  Execute Command  ls

Get Connections
    Open Connection   ${HOST}   prompt=>>
    Open Connection   ${HOST}   alias=saila
    ${conns} =   Get Connections
    Should Be Equal    ${conns[0].host}    ${HOST}
    Should Be Equal    ${conns[1].host}    ${HOST}
    Should Be Equal    ${conns[0].prompt}    >>
    Should Be Equal    ${conns[1].alias}    saila
    Should Be Equal    ${conns[0].term_type}    vt100

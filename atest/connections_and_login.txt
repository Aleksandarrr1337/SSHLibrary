*** Settings ***
Default Tags   pybot   jybot
Resource       resources/common.txt
Test Teardown  Close All Connections
Library        OperatingSystem

*** Test Cases ***
Open Connection
    Login As Valid User

Close Connection
    [Setup]  Login As Valid User
    Close Connection
    Connection Should Be Closed

Close Connection Fails If No Connection
    Run Keyword And Expect Error  No open connection.  Close Connection

Switch Connection
    ${conn1_id} =  Open Connection  ${HOST}  alias=one  prompt=${PROMPT}
    Login  ${USERNAME}  ${PASSWORD}
    Write  cd /tmp
    Read Until Prompt
    ${conn_id2} =  Open Connection  ${HOST}  alias=second  prompt=${PROMPT}
    Login  ${USERNAME}  ${PASSWORD}
    ${prev id} =  Switch Connection  ${conn1_id}
    Should Be Equal  ${prev id}  ${conn_id2}
    Write  pwd
    ${output} =  Read Until Prompt
    Should Contain  ${output}  /tmp
    ${prev id} =  Switch Connection  second
    Write  pwd
    ${result} =  Read Until Prompt
    Should Contain  ${result}  ~

Switch Connection To None
    [Setup]   Login As Valid User
    Switch Connection  ${NONE}
    Connection Should Be Closed

Login Returns Server Output
    [Setup]  Open Connection  ${HOST}
    ${output}=  Login  ${USERNAME}  ${PASSWORD}
    Should Contain  ${output}  Last login:
    ${output}=  Read
    Should Be Equal  ${output.strip()}  ${EMPTY}

Login Returns Server Output If Prompt Is Set
    [Setup]  Open Connection  ${HOST}  prompt=${PROMPT}
    ${output}=  Login  ${USERNAME}  ${PASSWORD}
    Should Contain  ${output}  Last login:
    ${output}=  Read
    Should Be Equal  ${output.strip()}  ${EMPTY}

Enable Logging
    [Tags]   pybot
    [Setup]  Remove File  ${OUTPUTDIR}${/}sshlog.txt
    Enable SSH Logging  ${OUTPUTDIR}${/}sshlog.txt
    Login As Valid User
    File Should Not Be Empty  ${OUTPUTDIR}${/}sshlog.txt

*** Keywords ***
Connection Should Be Closed
    Run Keyword And Expect Error  No open connection.  Write  pwd

*** Settings ***
Default Tags    pybot   jybot
Test Teardown   Close All Connections
Resource        resources/ssh_library_resources.txt
Library         OperatingSystem

*** Test Cases ***
Open Connection
    Login As Valid User

Close Connection
    [Setup]  Login As Valid User
    Close Connection
    Connection Should Be Closed

Close Connection Fails If No Connection
    Run Keyword And Expect Error  No open connection.  Close Connection

Switch Connection
    ${conn1_id} =  Login As Valid User  one
    Write  cd /tmp
    Read Until Prompt
    ${conn_id2} =  Login As Valid User  second
    ${prev id} =  Switch Connection  ${conn1_id}
    Should Be Equal  ${prev id}  ${conn_id2}
    Write  pwd
    ${output} =  Read Until Prompt
    Should Contain  ${output}  /tmp
    ${prev id} =  Switch Connection  second
    Write  pwd
    ${result} =  Read Until Prompt
    Should Contain  ${result}  ${USER HOME}

Switch Connection To None
    [Setup]   Login As Valid User
    Switch Connection  ${NONE}
    Connection Should Be Closed

Login Returns Server Output
    [Setup]  Open Connection  ${HOST}
    ${output}=  Login  ${USERNAME}  ${PASSWORD}
    Should Contain  ${output}  :~$

Login Returns Server Output Until Prompt If Prompt Is Set
    [Setup]  Open Connection  ${HOST}  prompt=$
    ${output}=  Login  ${USERNAME}  ${PASSWORD}
    Should Contain  ${output}  :~$

Enable Logging
    [Tags]   pybot
    [Setup]  Remove File  ${OUTPUTDIR}/sshlog.txt
    Enable SSH Logging  ${OUTPUTDIR}/sshlog.txt
    Open Connection  ${HOST}
    Login  ${username}  ${password}
    File Should Not Be Empty  ${OUTPUTDIR}/sshlog.txt

Get Connection
    ${conn1_index} =  Open Connection  ${HOST}  alias=one
    ${conn2_index} =  Open Connection  ${HOST}
    ${conn} =  Get Connection
    Should Be Equal  ${conn.index}  ${conn2_index}
    ${conn} =  Get Connection  one
    Should Be Equal  ${conn.index}  ${conn1_index}
    Close Connection
    ${conn} =  Get Connection  2
    Should Be Equal  ${conn.index}  ${conn2_index}

Get Connection When No Connection Is Open
    ${conn}=  Get Connection  1
    Should Be Equal   ${conn.host}               ${None}
    Should Be Equal   ${conn.index}              ${None}
    Should Be Equal   ${conn.alias}              ${None}
    Should Be Equal   ${conn.prompt}             ${None}
    Should Be Equal As Integers  ${conn.port}    22
    Should Be Equal   ${conn.newline}            \n
    Should Be Equal   ${conn.encoding}           utf8
    Should Be Equal   ${conn.term_type}          vt100
    Should Be Equal As Integers  ${conn.width}   80
    Should Be Equal As Integers  ${conn.height}  24

Get Connections
    Open Connection   ${HOST}   prompt=>>
    Open Connection   ${HOST}   alias=another
    ${conns} =   Get Connections
    Length Should Be   ${conns}  2
    Should Be Equal As Integers   ${conns[0].index}   1
    Should Be Equal As Integers   ${conns[1].index}   2
    Should Be Equal    ${conns[0].host}       ${HOST}
    Should Be Equal    ${conns[1].host}       ${HOST}
    Should Be Equal    ${conns[0].prompt}     >>
    Should Be Equal    ${conns[1].alias}      another
    Should Be Equal    ${conns[0].term_type}  vt100

Get Connections Returns Empty List When No Connections
    ${conns} =  Get Connections
    ${empty_list} =  Create List
    Should Be Equal  ${conns}  ${empty_list}

*** Keywords ***
Connection Should Be Closed
    Run Keyword And Expect Error  No open connection.  Write  pwd

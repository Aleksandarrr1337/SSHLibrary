*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login and Move Test Files
Suite Teardown  Remove Test Files and Close Connections
Resource        resources/ssh_library_resources.txt
Library         Collections

*** Test Cases ***
List Directory Using Absolute Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${EMPTY TEXTFILES NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    ${listing} =  List Directory  ${target}
    Lists Should Be Equal  ${listing}  ${expected}

List Directory Using Relative Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${EMPTY TEXTFILES NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  ${TEST_ROOT_NAME}
    ${listing} =  List Directory  ${target}
    Lists Should Be Equal  ${listing}  ${expected}

List Directory Using Current Directory
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${EMPTY TEXTFILES NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  .
    ${listing} =  List Directory  ${target}
    Should Contain  ${listing}  ${TEST_ROOT_NAME}

List Directory With Absolute Paths Using Absolute Path
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${EMPTY TEXTFILES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NEWLINES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${SPECIAL TEXTFILES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    ${listing} =  List Directory  ${target}  absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

List Directory With Absolute Paths Using Relative
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${EMPTY TEXTFILES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${FILE WITH NEWLINES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${SPECIAL TEXTFILES NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${TEST_ROOT_NAME}
    ${listing} =  List Directory  ${target}  absolute=True
    Lists Should Be Equal  ${listing}  ${EXPECTED}

List Directory With Absolute Paths Using Current Directory
    ${target} =  Set Variable  .
    ${listing} =  List Directory  ${target}  absolute=True
    Should Contain  ${listing}  ${REMOTE TEST ROOT}

List Directory Using Pattern
    ${expected} =  Create List  ${SPECIAL TEXTFILES NAME}
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    ${listing} =  List Directory  ${target}  pattern=spec*
    Lists Should Be Equal  ${listing}  ${expected}

List Directory With Absolute Paths Using Pattern
    ${expected} =  Create List
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE 2 NAME}
    ...                 ${REMOTE TEST ROOT}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    ${listing} =  List Directory  ${target}  pattern=?est*  absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

List Directory Should Fail When Source Path Not Exists
    ${target} =  Set Variable  not_exists
    Run Keyword And Expect Error  There was no path matching '${target}'  List Directory  ${target}

*** Keywords ***
Login And Move Test Files
    Login As Valid User
    Put Directory  ${TEXTFILES}  ${REMOTE TEST ROOT}  recursive=True

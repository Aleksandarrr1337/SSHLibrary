*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login and Move Test Files
Suite Teardown  Remove Test Files and Close Connections
Resource        resources/ssh_library_resources.txt
Library         Collections

*** Variables ***
${TEST_ROOT_NAME}           test_root
${ABSOLUTE_TEST_ROOT_PATH}  ${USER HOME}${/}${TEST_ROOT_NAME}

*** Test Cases ***
List Content Using Absolute Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  ${ABSOLUTE_TEST_ROOT_PATH}
    ${listing} =  List Directory  ${target}
    Lists Should Be Equal  ${listing}  ${expected}

List Content Using Relative Path
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  ${TEST_ROOT_NAME}
    ${listing} =  List Directory  ${target}
    Lists Should Be Equal  ${listing}  ${expected}

List Content Using Current Directory
    ${expected} =  Create List  ${TEST FILE 2 NAME}  ${FILE WITH NEWLINES NAME}
    ...                         ${SPECIAL TEXTFILES NAME}  ${TEST FILE NAME}
    ${target} =  Set Variable  .
    ${listing} =  List Directory  ${target}
    Should Contain  ${listing}  ${TEST_ROOT_NAME}

List Content With Absolute Paths Using Absolute Path
    ${expected} =  Create List
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE 2 NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${FILE WITH NEWLINES NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${SPECIAL TEXTFILES NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${ABSOLUTE_TEST_ROOT_PATH}
    ${listing} =  List Directory  ${target}  absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

List Content With Absolute Paths Using Relative
    ${expected} =  Create List
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE 2 NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${FILE WITH NEWLINES NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${SPECIAL TEXTFILES NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${TEST_ROOT_NAME}
    ${listing} =  List Directory  ${target}  absolute=True
    Lists Should Be Equal  ${listing}  ${EXPECTED}

List Content With Absolute Paths Using Current Directory
    ${target} =  Set Variable  .
    ${listing} =  List Directory  ${target}  absolute=True
    Should Contain  ${listing}  ${ABSOLUTE_TEST_ROOT_PATH}

List Content Using Pattern
    ${expected} =  Create List  ${SPECIAL TEXTFILES NAME}
    ${target} =  Set Variable  ${ABSOLUTE_TEST_ROOT_PATH}
    ${listing} =  List Directory  ${target}  pattern=spec*
    Lists Should Be Equal  ${listing}  ${expected}

List Content With Absolute Paths Using Pattern
    ${expected} =  Create List
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE 2 NAME}
    ...                 ${ABSOLUTE_TEST_ROOT_PATH}${/}${TEST FILE NAME}
    ${target} =  Set Variable  ${ABSOLUTE_TEST_ROOT_PATH}
    ${listing} =  List Directory  ${target}  pattern=?est*  absolute=True
    Lists Should Be Equal  ${listing}  ${expected}

*** Keywords ***
Login And Move Test Files
    Login As Valid User
    Put Directory  ${TEXTFILES}  ${ABSOLUTE_TEST_ROOT_PATH}  recursive=True

Remove Test Files and Close Connections
    Execute Command  rm -rf ${ABSOLUTE_TEST_ROOT_PATH}
    Close All Connections

*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login and Move Test Files
Suite Teardown  Remove Test Files And Close Connections
Test Teardown   Remove Directory  ${LOCAL TMPDIR}  yes
Resource        resources/ssh_library_resources.txt
Library         OperatingSystem  WITH NAME  OS

*** Test Cases ***
Get File From Absolute Source
    OS.File Should Not Exist  ${LOCAL TMPDIR}${/}${TEST FILE NAME}
    SSH.Get File  ${REMOTE TEST ROOT}${/}${TEST FILE NAME}  ${LOCAL TMPDIR}${/}
    OS.File Should Exist  ${LOCAL TMPDIR}${/}${TEST FILE NAME}
    [Teardown]  OS.Remove File  ${LOCAL TMPDIR}${/}${TEST FILE NAME}

Get File From Relative Source
    OS.File Should Not Exist  ${TEST FILE NAME}
    SSH.Get File  ${TEST_ROOT_NAME}${/}${TEST FILE NAME}  ${TEST FILE NAME}
    OS.File Should Exist  ${TEST FILE NAME}
    [Teardown]  OS.Remove File  ${TEST FILE NAME}

Get File With Different Name
    SSH.Get File  ${REMOTE TEST ROOT}${/}${TEST FILE NAME}  foo.txt
    OS.File Should Exist  foo.txt
    [Teardown]  OS.Remove File  foo.txt

Get File With Pattern
    @{expected} =  Create List  ${TEST FILE NAME}  ${TEST FILE 2 NAME}
    ...                         ${FILE WITH NEWLINES NAME}
    ${destination} =  Set Variable  ${LOCAL TMPDIR}${/}
    : FOR  ${filename}  IN  @{expected}
    \  OS.File Should Not Exist  ${destination}${/}${filename}
    SSH.Get File  ${REMOTE TEST ROOT}/*  ${destination}
    : FOR  ${filename}  IN  @{expected}
    \  OS.File Should Exist  ${destination}${/}${filename}

Getting Multiple Source Files To Single File Fails
    Run Keyword And Expect Error
    ...  Cannot copy multiple source files to one destination file.
    ...  SSH.Get File  ${REMOTE TEST ROOT}/*.txt  ${LOCAL TMPDIR}${/}foo

Get File To Curdir
    SSH.Get File  ${REMOTE TEST ROOT}${/}${TEST FILE NAME}  .
    OS.File Should Exist  ${TEST_FILE_NAME}
    [Teardown]  OS.Remove File  ${TEST FILE NAME}

Get File When Destination Directory Does Not Exist
    ${target} =  Set Variable  ${LOCAL TMPDIR}${/}missingdir${/}foo.txt
    SSH.Get File  ${REMOTE TEST ROOT}${/}${TEST FILE NAME}  ${target}
    OS.File Should Exist  ${target}
    [Teardown]  OS.Remove File  ${target}

Get File From A Path Not Under Home Directory
    [Setup]  Create Tmp Dir And Move File
    SSH.Get File  /tmp/test_file.txt  ${LOCAL TMPDIR}${/}
    OS.File Should Exist  ${LOCAL TMPDIR}${/}test_file.txt
    [Teardown]  Remove Tmp Dir And Remote File

Get File To Absolute Destination
    SSH.Get File  ${REMOTE TEST ROOT}${/}${TEST FILE NAME}  ${LOCAL TMPDIR}${/}
    OS.File Should Exist  ${LOCAL TMPDIR}${/}${TEST_FILE_NAME}
    [Teardown]  OS.Remove Directory  ${LOCAL TMPDIR}  yes

Get File Should Fail When There Are No Source Files
    Run Keyword And Expect Error
    ...  There were no source files matching 'non-existing'
    ...  SSH.Get File  non-existing

*** Keywords ***
Create Tmp Dir And Move File
    Put File  ${TEST FILE}  /tmp/
    Create Directory  ${LOCAL TMPDIR}

Remove Tmp Dir And Remote File
    Execute Command  rm -f /tmp/test_file.txt
    Remove Directory  ${LOCAL TMPDIR}  yes




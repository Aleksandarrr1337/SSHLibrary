*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login As Valid User
Suite Teardown  Close All Connections
Test Teardown   Remove Remote Files  ${target}
Resource        resources/ssh_library_resources.txt

*** Test Cases ***
Put Directory To Existing Remote Path
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    Verify Remote Directory Exists  ${target}
    Put Directory  ${TEXTFILES}  ${target}
    ${target} =  Set Variable  ${target}${/}textfiles
    Remote Directory Should Exist With Content  ${target}

Put Directory To Non-Existing Remote Path
    ${target} =  Set Variable  another_dir_name
    Verify Remote Directory Does Not Exists  ${target}
    Put Directory  ${TEXTFILES}  ${target}
    Remote Directory Should Exist With Content  ${target}

Put Directory Including Subdirectories To Existing Remote Path
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    Verify Remote Directory Exists  ${target}
    Put Directory  ${TEXTFILES}  ${target}  recursive=True
    ${target} =  Set Variable  ${target}${/}textfiles
    Remote Directory Should Exist With Subdirectories  ${target}

Put Directory Including Subdirectories To Non-Existing Remote Path
    ${target} =  Set Variable  another/dir/path
    Verify Remote Directory Does Not Exists  ${target}
    Put Directory  ${TEXTFILES}  ${target}  recursive=True
    Remote Directory Should Exist With Subdirectories  ${target}
    ${target} =  Set Variable  another

Put Directory To Current Remote Path
    ${target} =  Set Variable  .
    Verify Remote Directory Exists  ${target}
    Put Directory  ${TEXTFILES}  ${target}
    ${target} =  Set Variable  ${target}${/}textfiles
    Remote Directory Should Exist With Content  ${target}

Put Directory Using Relative Source
    ${target} =  Set Variable  ${REMOTE TEST ROOT}
    Verify Remote Directory Exists  ${target}
    Put Directory  textfiles  ${target}
    ${target} =  Set Variable  ${target}${/}textfiles
    Remote Directory Should Exist With Content  ${target}

Put Directory Should Fail When Source Does Not Exists
    ${target} =  Set Variable  another_dir_name
    Run Keyword And Expect Error  There was no source path matching 'non-existing'  Put Directory  non-existing
    Verify Remote Directory Does Not Exists  ${target}

*** Keywords ***
Verify Remote Directory Exists
    [Arguments]  ${destination}
    SSH.Directory Should Exist  ${destination}

Verify Remote Directory Does Not Exists
    [Arguments]  ${destination}
    SSH.Directory Should Not Exist  ${destination}

Remote Directory Should Exist With Content
    [Arguments]  ${destination}
    SSH.File Should Exist  ${destination}${/}${TEST FILE NAME}
    SSH.File Should Exist  ${destination}${/}${TEST FILE 2 NAME}
    SSH.File Should Exist  ${destination}${/}${FILE WITH NEWLINES NAME}
    SSH.Directory Should Not Exist  ${destination}${/}${SPECIAL TEXTFILES NAME}

Remote Directory Should Exist With Subdirectories
    [Arguments]  ${destination}
    SSH.File Should Exist  ${destination}${/}${TEST FILE NAME}
    SSH.File Should Exist  ${destination}${/}${TEST FILE 2 NAME}
    SSH.File Should Exist  ${destination}${/}${FILE WITH NEWLINES NAME}
    SSH.Directory Should Exist  ${destination}${/}${SPECIAL TEXTFILES NAME}
    SSH.File Should Exist  ${destination}${/}${SPECIAL TEXTFILES NAME}${/}${FILE WITH SPECIAL CHARS NAME}

Remove Remote Files
    [Arguments]  ${destination}
    Execute Command  rm -rf ${destination}

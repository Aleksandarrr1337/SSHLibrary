*** Settings ***
Force Tags      pybot   jybot
Suite Setup     Login And Move Test Files
Suite Teardown  Remove Test Files and Close Connections
Resource        resources/ssh_library_resources.txt

*** Variables ***
${COUNTER NAME}  counter.txt

*** Test Cases ***
Read
    Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    Write  ääöö
    ${output} =  Read
    Should Contain  ${output}  Hello ääöö

Read Until Prompt
    Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    Write  ääöö
    ${output} =  Read Until Prompt
    Should Contain  ${output}  Hello ääöö

Read Until Regexp
    Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    Write  ääöö
    ${output} =  Read Until Regexp  \n
    Should Contain  ${output}  Hello ääöö

Basic Interactive Session
    ${output} =  Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    Should Contain  ${output}  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    ${output} =  Read Until  Give your name?
    Should Contain  ${output}  Give your name?
    ${output} =  Write  Juha
    Should Contain  ${output}  Juha
    ${output} =  Read Until Prompt
    Should Contain  ${output}  Hello Juha

Interactive Session With Regular Expressions
    Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    ${output} =  Read Until Regexp  Give.*\\?
    Should Contain  ${output}  Give your name?
    Write  Juha
    Comment  Prompt needs to be escaped because it might be $
    ${output} =  Read Until Regexp  (?s).*\\${PROMPT}
    Should Contain  ${output}  Hello Juha
    Should End With  ${output}  ${PROMPT}

Interactive Session In Case Of Timeout
    Write  Foo Bar And Some Other
    Set Client Configuration   timeout=1
    ${status}  ${error} =  Run Keyword And Ignore Error
    ...                    Read Until  This is not found
    Should Start With  ${error}  No match found for 'This is not found' in 1 second

Interactive Session Returning Stderr
    Write  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}
    Read Until  Give your name?
    Write  Error
    ${output} =  Read Until  ${PROMPT}
    Should Contain  ${output}  Hello Error
    Should Contain  ${output}  This is Error

Write Bare And Read
    Write Bare  ${REMOTE TEST ROOT}${/}${INTERACTIVE TEST SCRIPT NAME}\n
    ${output} =  Read Until  name?
    Write Bare  Janne\n
    ${output2} =  Read Until Prompt
    Should Contain  ${output}  Give your name?
    Should Contain  ${output2}  Hello Janne
    Should Contain  ${output2}  ${PROMPT}

Write Until Expected Output
    Write Until Expected Output  ${REMOTE TEST ROOT}${/}${REPEAT TEST SCRIPT NAME}\n
    ...                          3  15 seconds  0.5 seconds
    [Teardown]  Remove Counter

Write Until Expected Output In Case Of Timeout
    Run Keyword And Expect Error  No match found for '11' in 2 seconds.
    ...                           Write Until Expected Output  ${REMOTE TEST ROOT}${/}${REPEAT TEST SCRIPT NAME}\n
    ...                           11  2s  0.5s
    [Teardown]  Remove Counter

*** Keywords ***
Login And Move Test Files
    Login As Valid User
    Put File  ${SCRIPTS}/*  ${REMOTE TEST ROOT}${/}

Remove Counter
    Execute Command  rm -f ${COUNTER NAME}

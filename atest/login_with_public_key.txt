*** Settings ***
Force Tags      pybot   jybot
Test Setup      Open Connection  ${HOST}
Test Teardown   Close All Connections
Resource        resources/ssh_library_resources.txt
Library         OperatingSystem  WITH NAME  OS

*** Variables ***
${PUBKEYDIR}                ${CURDIR}/testdata/keyfiles
${PUBKEY USERNAME}          testkey
${PUBKEY FILE}              ${PUBKEYDIR}/id_rsa
${INVALID PUBKEY USERNAME}  invalid_key_username
${INVALID PUBKEY FILE}      ${PUBKEYDIR}/id_rsa_invalid

*** Test Cases ***
Login With Public Key
    Open Connection  ${HOST}  prompt=${PROMPT}
    Login With Public Key  ${PUBKEY USERNAME}  ${PUBKEY FILE}

Login With Non Existing Public Key
    Run Keyword And Expect Error  Given key file '/invalid/path' does not exist.
    ...                           Login With Public Key  ${PUBKEY USERNAME}
    ...                           /invalid/path

Login With Invalid Public Key
    Run Keyword And Expect Error  Login with public key failed for user 'testkey'.
    ...    Login With Public Key  ${PUBKEY USERNAME}  ${INVALID PUBKEY FILE}

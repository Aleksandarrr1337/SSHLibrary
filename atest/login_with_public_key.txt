*** Settings ***
Force Tags     pybot   jybot
Resource       resources/common.txt
Test Setup     Open Connection  ${HOST}
Test Teardown  Close All Connections

*** Variables ***
${VALID USERNAME}    testkey
${INVALID USERNAME}  invalid
${KEY DIR}           ${LOCAL TESTDATA}${/}keyfiles
${VALID KEY}         ${KEY DIR}${/}id_rsa
${INVALID KEY}       ${KEY DIR}${/}id_rsa_invalid

*** Test Cases ***
Login With Valid Username And Key
    Open Connection  ${HOST}  prompt=${PROMPT}
    Login With Public Key  ${VALID USERNAME}  ${VALID KEY}

Login With Invalid Username
    Run Keyword And Expect Error  Login with public key failed for user 'invalid'.
    ...    Login With Public Key  ${INVALID USERNAME}  ${VALID KEY}

Login With Invalid Key
    Run Keyword And Expect Error  Login with public key failed for user 'testkey'.
    ...    Login With Public Key  ${VALID USERNAME}  ${INVALID KEY}

Login With Non-Existing Key
    Run Keyword And Expect Error  Given key file 'not_existing_key' does not exist.
    ...    Login With Public Key  ${VALID USERNAME}  not_existing_key
